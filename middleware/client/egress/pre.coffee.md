First-line handler for outbound calls
-------------------------------------

    seem = require 'seem'
    pkg = require '../../../package.json'
    @name = "#{pkg.name}:middleware:client:egress:pre"
    debug = (require 'tangible') @name

    @include = seem ->

      return unless @session.direction is 'egress'

      music_uri = (doc) =>
        return null unless doc.music?
        @prompt.uri 'prov', 'prov', doc._id, doc.music

Endpoint
--------
* session.endpoint_name (string) The name of the calling endpoint in an egress call, set from hdr.X-En

      @session.endpoint_name = @req.header 'X-En'

Endpoint might be provided in the reference data for example for an `originate` call (in exultant-songs).

      @session.endpoint_name ?= yield @reference.get_endpoint()

      unless @session.endpoint_name?
        @debug.dev 'Missing endpoint_name', @call.data
        return @respond '485 Missing Endpoint Header'

      @debug 'endpoint', @session.endpoint_name
      yield @reference.set_endpoint @session.endpoint_name

      yield @unset 'sip_h_X-En'

* session.endpoint (object) Data from the calling `doc.endpoint` (also known as the `doc.src_endpoint`) in an egress call.

      @session.endpoint = yield @cfg.prov.get "endpoint:#{@session.endpoint_name}"
      yield @reference.add_in @session.endpoint._id
      if @session.endpoint.timezone?
        @session.timezone = @session.endpoint.timezone
      if @session.endpoint.music?
        @session.music = music_uri @session.endpoint
      if @session.endpoint.trace
        @session.dev_logger = true

      @session.agent = @session.endpoint_name
      @session.agent_name = @session.endpoint.display_name ? @source

      @set
        ccnq_endpoint: @session.endpoint_name
        ccnq_endpoint_json: JSON.stringify @session.endpoint

Outbound-route
--------------

      @session.outbound_route = @session.endpoint.outbound_route
      unless @session.outbound_route?
        @debug.dev 'Missing outbound_route', @session.endpoint
        return @respond '500 Endpoint has no outbound_route'

      @debug 'outbound_route', @session.outbound_route

Number-domain
-------------

Note: The header is no longer generated by ccnq4-opensips.

* doc.number_domain On egress calls, the number-domain is selected based on the doc.src_endpoint.number_domain field.
* doc.src_endpoint.number_domain (required)

      number_domain = @session.endpoint.number_domain
      unless number_domain?
        @debug.dev 'Missing number_domain', @session.endpoint
        return @respond '480 Missing Number Domain'

* session.number_domain Name of the number-domain (egress calls).

      @session.number_domain = number_domain

The `number_domain` field is required, but the number-domain record is optional.

* session.number_domain_data Data record of the number-domain (egress calls).

      @session.number_domain_data = yield @cfg.prov
        .get "number_domain:#{number_domain}"
        .catch (error) =>
          @debug "number_domain #{number_domain}: #{error}"
          {}

      @debug 'number_domain', number_domain
      yield @reference.add_in @session.number_domain_data._id
      yield @user_tags @session.number_domain_data.tags

Number-domain is less specific than endpoint, so do not override.

      if @session.number_domain_data.timezone?
        @session.timezone ?= @session.number_domain_data.timezone
      if @session.number_domain_data.music?
        @session.music ?= music_uri @session.number_domain_data

Source (calling) number
-----------------------

* session.number (object) Data from doc.number for the calling number in an egress call.

      src_number = "#{@source}@#{number_domain}"
      if @session.transfer
        @session.number = {}
      else
        @session.number = yield @cfg.prov
          .get "number:#{src_number}"

On a static trunk, the number might not be present.

          .catch (error) =>
            @debug.ops "number:#{src_number}: #{error}"
            {}

      if @session.number._id?
        yield @reference.add_in @session.number._id

Number is more specific than endpoint or number-domain, override.

      if @session.number.timezone?
        @session.timezone = @session.number.timezone
      if @session.number.music?
        @session.music = music_uri @session.number
      if @session.number.trace
        @session.dev_logger = true
      if @session.number.record_egress
        @record_call @session.number._id

Dialplan
--------

* doc.local_number.dialplan (string) Dialplan used for egress calls. Generally `national` or `centrex`.
* doc.src_endpoint.dialplan (string) Dialplan used for egress calls if doc.local_number.dialplan is empty.
* doc.number_domain.dialplan (string) Dialplan used for egress calls if doc.local_number.dialplan and doc.src_endpoint.dialplan is empty.

      @session.dialplan = @session.number.dialplan
      @session.dialplan ?= @session.endpoint.dialplan
      @session.dialplan ?= @session.number_domain_data.dialplan

Country
-------

* doc.local_number.country (string) Country used for egress calls.
* doc.src_endpoint.country (string) Country used for egress calls if doc.local_number.country is empty.
* doc.number_domain.country (string) Country used for egress calls if doc.local_number.country and doc.src_endpoint.country is empty.

      @session.country = @session.number.country
      @session.country ?= @session.endpoint.country
      @session.country ?= @session.number_domain_data.country

Contrarily to established practices, our code uses lowercase country names.

      if @session.country?
        @session.country = @session.country.toLowerCase()

Eavesdrop registration
----------------------

      key = src_number
      eavesdrop_key = "outbound:#{key}"

      unless @session.transfer or @call.closed

        @debug 'Set outbound eavesdrop', eavesdrop_key
        yield @local_redis?.set eavesdrop_key, @call.uuid

        attributes = {key,id:@call.uuid,dialplan:@session.dialplan}

        when_done = seem =>
          debug 'Clear outbound eavesdrop', eavesdrop_key, attributes
          @emit 'outbound-end', attributes
          yield @local_redis?.del eavesdrop_key
          return

        @call.once 'socket-close', when_done
        @once 'tough-rate-hangup', when_done

        yield @call.event_json 'CHANNEL_HANGUP_COMPLETE'
        @call.once 'CHANNEL_HANGUP_COMPLETE', when_done

        @emit 'outbound', attributes

      @debug 'Ready',
        endpoint_name: @session.endpoint_name
        outbound_route: @session.outbound_route
        number_domain: @session.number_domain
        dialplan: @session.dialplan
        country: @session.country

Location
--------

This is needed for emergency call routing.

* doc.local_number.location (string) Emergency call routing location.
* doc.src_endpoint.location (string) Emergency call routing location (if no doc.local_number.location is provided).

      location = @session.number.location
      location ?= @session.endpoint.location
      location ?= ''
      @session.emergency_location = location

Privacy
-------

Enforce configurable privacy settings.

* doc.local_number.privacy (boolean) Whether privacy is requested for egress calls.
* doc.src_endpoint.privacy (boolean) Whether privacy is requested for egress calls (if doc.local_number.privacy is not set).

      privacy = @session.number.privacy
      privacy ?= @session.endpoint.privacy
      if privacy
        yield @action 'privacy', 'number'
      else
        yield @action 'privacy', 'no'

Asserted-Number
---------------

Enforce configurable Caller-ID. (Used in particular for ported-in numbers.)

* doc.local_number.asserted_number (string) Asserted number for egress calls.
* doc.src_endpoint.asserted_number (string) Asserted number for egress calls (if doc.local_number.asserted_number is not set).

      @session.asserted = @session.number.asserted_number
      @session.asserted ?= @session.endpoint.asserted_number

Check from
----------

Optionally enforce that the calling number originates from the associated endpoint (useful e.g. to prevent invalid caller-id from static endpoints).

* doc.src_endpoint.check_from (boolean) If true, doc.local_number.endpoint must match the originating endpoint's name. This is used to restrict originating numbers on an endpoint to those that are provisioned on that endpoint.

      if @session.endpoint.check_from
        if @session.number.endpoint isnt @session.endpoint_name
          @debug 'From Username is not listed'
          return @respond '403 From Username is not listed'

ICE
---

For backward-compatibility we currently ignore ICE proposals.

      yield @set ignore_sdp_ice: @session.endpoint.ignore_sdp_ice ? true

      @report
        state:'egress-received'
        endpoint: @session.endpoint._id
        number_domain: @session.number_domain_data._id
        number: @session.number._id ? null
      @debug 'OK'
      return
